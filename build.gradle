buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

allprojects {
    apply plugin: 'idea'

    group 'com.huaouo'
    version '0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    if (project.name != 'stormy-shared') {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        implementation 'org.apache.commons:commons-lang3:3.9'
        implementation 'io.lettuce:lettuce-core:5.2.2.RELEASE'
        if (project.name != 'stormy-client') {
            implementation 'org.slf4j:slf4j-api:1.7.30'
            implementation 'ch.qos.logback:logback-classic:1.2.3'
            implementation 'com.google.inject:guice:4.2.2'
        }

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    }

    test {
        useJUnitPlatform()
    }

    if (project.name != 'stormy-shared') {
        shadowJar {
            def projectSuffix = project.name.split('-')[1]

            archiveFileName.set("${projectSuffix}.jar")

            manifest {
                attributes(
                        'Main-Class': "com.huaouo.stormy.${projectSuffix}.${projectSuffix.capitalize()}Main"
                )
            }
        }
    }
}
