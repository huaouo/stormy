buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

allprojects {
    apply plugin: 'idea'

    group 'com.huaouo'
    version '0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    if (!project.name.endsWithAny('shared', 'api')) {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        if (!project.name.endsWithAny('api', 'client', 'example')) {
            if (!project.name.endsWith('shared')) {
                implementation 'ch.qos.logback:logback-classic:1.2.3'
            }
            implementation 'com.google.inject:guice:4.2.3'
        }

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    }

    test {
        useJUnitPlatform()
    }

    if (project.name.endsWithAny('master', 'worker', 'client', 'ui')) {
        shadowJar {
            def projectSuffix = project.name.split('-')[1]

            archiveFileName.set("${projectSuffix}.jar")

            manifest {
                attributes(
                        'Main-Class': "com.huaouo.stormy.${projectSuffix}.${projectSuffix.capitalize()}Main"
                )
            }
        }
    }
}
